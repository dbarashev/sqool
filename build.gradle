buildscript {
	ext {
		kotlinVersion = '1.5.+'
	}
	repositories {
		google()
		mavenCentral()
	}
	dependencies {
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
	}
}
plugins {
	id 'com.google.cloud.tools.jib' version '2.5.0'
//	id "name.remal.check-updates" version "1.0.157"
}

apply plugin: 'application'
apply plugin: 'kotlin'

version = '1.0.4'
sourceCompatibility = 1.8

ext {
	buildMode = findProperty('buildMode') ?: 'development'
}
repositories {
	mavenCentral()
}

mainClassName = 'com.bardsoftware.sqool.contest.AppSparkKt'

dependencies {
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
	implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"

	implementation "com.xenomachina:kotlin-argparser:2.+"

	implementation group: 'com.spotify', name: 'docker-client', version: '8.+'

	implementation 'org.ocpsoft.prettytime:prettytime:4.+'

	// PubSub stuff
	implementation 'com.google.cloud:google-cloud-core:1.+'
	implementation 'com.google.cloud:google-cloud-pubsub:1.+'

	// Postgres stuff
	implementation 'org.postgresql:postgresql:42.+'
	implementation 'com.zaxxer:HikariCP:2.+'
	implementation 'org.jetbrains.exposed:exposed:0.17.+'
	implementation "org.jooq:jooq:3.+"

	// Spark stuff
	implementation "com.sparkjava:spark-kotlin:1.+"
	implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.+"
	implementation "com.sparkjava:spark-template-freemarker:2.+"

	implementation 'org.apache.httpcomponents:fluent-hc:4.+'

	implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.2.2'
	implementation 'com.vladsch.flexmark:flexmark-all:0.+'
	implementation "org.telegram:telegrambots:5.+"
	implementation "org.telegram:telegrambotsextensions:5.+"
	implementation "com.michael-bull.kotlin-result:kotlin-result:1.1.16"
	implementation "io.ktor:ktor-client-core:2.3.4"
	implementation "io.ktor:ktor-client-cio:2.3.4"

	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.+'
	testImplementation group: 'com.nhaarman', name: 'mockito-kotlin', version: '1.+'
}

sourceSets {
	main {
		kotlin {
			srcDirs += "$buildDir/sqldelight/ContestDb"
			srcDirs += "$buildDir/generated-src/jooq/main"
		}
	}
	contestImageTester {
		kotlin {
			srcDirs += "$projectDir/src/contest-image-tester/kotlin"
		}
	}
	sql {
		resources {
			srcDir "$projectDir/src/main/sql"
		}
	}
}

// Compiles TypeScript sources and makes webpack bundle
task buildClient(type: Exec) {
	workingDir = "$projectDir"
	doFirst {
		println("Building client")
		commandLine = 'npm'
		if (project.buildMode == "development") {
			args = ['run', 'build', '--', '--mode', project.buildMode, "--dest", "src/main/resources/public/js"]
		} else {
			args = ['run', 'build', '--', '--mode', project.buildMode, "--dest", "src/main/resources/"]
		}
	}
	doLast {
		println("TypeScript and Webpack done")
	}
}

assemble {
	dependsOn buildClient
}


test {
	useJUnitPlatform()
	testLogging {
        	exceptionFormat = 'full'
	}
	systemProperty "postgres.ip", project.properties["postgres.ip"] ?: "localhost"
}

jib {
	to.image = 'dbarashev/sqool-frontend'
	from.image = 'gcr.io/distroless/java:debug'
	container {
		environment = ["GOOGLE_APPLICATION_CREDENTIALS": "/var/run/sqool/dbms-class-2017-eb9a6787a012.json"]
		args = ["--pub", "dbms2019-submissions", "--sub", "dbms2019-frontend"]
	}
	extraDirectories {
		paths {
			path {
				from = file("src/main/resources/workspace")
				into = "/workspace"
			}
			path {
				from = file("src/main/jib/var/run/sqool/")
				into = "/var/run/sqool"
			}
		}
		permissions = [
				'/workspace': '777',
		]
	}
}

task docker {
	doLast {
		mkdir "$buildDir/docker/workspace"
		copy {
			from sourceSets.sql.resources.srcDirs
			into "$buildDir/docker/workspace"
			include "*.sql"
		}
		exec {
			workingDir "$projectDir"
			executable 'docker'
			args 'build','-t', 'dbarashev/sqool-frontend-database', '-f', 'sqool-frontend-database.dockerfile', '.'
		}
	}
}
